loadConfiguration()

def loadConfiguration() {
    ext {
        try {
           environment = env
        } catch (MissingPropertyException mpe) {
            environment = "dev"
        }
    }
    System.setProperty('whelk.config.uri', System.getProperty('whelk.config.uri', 'file:etc/environment/' + environment + '/whelk.json'))
    System.setProperty('plugin.config.uri', System.getProperty('plugin.config.uri', 'file:etc/plugins.json'))
    def encoding = hasProperty('file.encoding') ? file.encoding : 'UTF-8'
    System.setProperty 'file.encoding', encoding
}

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = "UTF-8"
}

tasks.withType(Compile) {
    options.encoding = "UTF-8"
}

allprojects {
    version "0.5.0"


    apply plugin: 'java'
    apply plugin: 'groovy'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    sourceSets {
        main {
            java { srcDirs = [] }
            groovy { srcDir 'src/main/' }
        }
        test {
            groovy { srcDir 'src/test/groovy/' }
        }
    }

    test.testLogging.exceptionFormat "full"

    repositories {
        mavenCentral()
        maven { url "http://maven.restlet.org/" }
        maven { url "http://repository.codehaus.org/" }
        flatDir name: 'localDir', dirs: '../lib'
    }

}

subprojects {

    task copyConfiguration << {
        println "Target environment: ${environment}"

        copy {
            from "../etc/environment/${environment}"
            into "src/main/resources"
            include "**/*"
        }
    }

    processResources.dependsOn copyConfiguration

    dependencies {
        groovy 'org.codehaus.groovy:groovy:2.0.4'
        groovy 'commons-cli:commons-cli:1.2'
        compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.12'
        compile 'ch.qos.logback:logback-classic:1.0.0'
        compile 'commons-io:commons-io:2.4'
        compile 'commons-codec:commons-codec:1.7'
        testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
        testCompile 'cglib:cglib-nodep:2.2'
    }
}

project(":locgov-pairtree") {
    dependencies {
        testCompile 'junit:junit:4.4'
    }
}
