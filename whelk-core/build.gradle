apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jetty'
apply plugin: 'application'


tasks.withType(GroovyCompile) {
  groovyOptions.encoding = 'utf-8'
}

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDir 'src/main/' }
    }
    test {
        groovy { srcDir 'src/test/groovy/' }
    }
}

test {
    exclude '**/RiakStorageSpec.class'
}


run {
    systemProperty "file.encoding", "UTF-8"
}

mainClassName = 'se.kb.libris.whelks.WhelkOperator'

jar {
    dependsOn configurations.runtime
        from {
            configurations.runtime.collect {
                it.isDirectory() ? it : zipTree(it).matching {
                    exclude "META-INF/OSGI*"
                }
            }
        }

    manifest {
        attributes 'Main-Class': mainClassName
    }

}

task depjar(type: Jar, dependsOn: jar) {
    archiveName = "dependencies.jar"
    dependsOn configurations.runtime
        from {
            configurations.runtime.collect {
                it.isDirectory() ? it : zipTree(it).matching {
                    exclude "se/kb/libris/**"
                    exclude "META-INF/**"
                }
            }
        }
}

// Currently using regular jar task for fatjar
//task fatjar(type: Jar, dependsOn: jar) { ... }

apply from: 'tools.gradle'

repositories {
    mavenCentral()
    maven { url "http://oss.sonatype.org/content/repositories/releases/" }
    maven { url "http://maven.restlet.org/" }
    maven { url "http://repository.codehaus.org/" }
    flatDir name: 'localDir', dirs: 'lib'
}

dependencies {
    groovy 'org.codehaus.groovy:groovy:2.0.4'
    groovy 'commons-cli:commons-cli:1.2'
    compile 'org.python:jython-standalone:2.5.2'
    compile 'org.mozilla:rhino:1.7R4'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'org.elasticsearch:elasticsearch:0.20.1'
    compile 'org.elasticsearch:elasticsearch-lang-groovy:1.1.0'
    compile 'com.spatial4j:spatial4j:0.3'
    compile 'ch.qos.logback:logback-classic:1.0.0'
    compile 'org.slf4j:log4j-over-slf4j:1.6.5'
    compile 'commons-io:commons-io:2.3'
    compile group:'org.restlet.jse', name:'org.restlet', version:'2.0.15'
    compile 'org.restlet.jee:org.restlet.ext.servlet:2.0.15'
    compile 'se.kb.libris:marc:1.2.7'
    compile 'jyson:jyson:1.0.2'
    compile 'commons-codec:commons-codec:1.7'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.9'
    compile 'com.basho.riak:riak-client:1.0.6'
    providedCompile 'javax.servlet:servlet-api:2.5'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}
